// возвращает map(columnName, SysDictField) - привязки к полям для колонок resultSet
// для каждого ключа [tableId, formatColumnName] хранится отдельный map
//
// если для данного ключа map еще не создавался, то создаст map,
// содержащий все возможные пары, для которых названия и типы совпадают
Map binds(Common rec, str formatColumnName = "")
{
    container       key     = [rec.TableId, formatColumnName];
    Map             binds;
    str             columnName;
    SysDictTable    dt;
    int             i;
    SysDictField    df;

    if( !bindsAll )
        bindsAll = new Map(Types::Container, types::Class);

    if( bindsAll.exists(key) )
        return bindsAll.lookup(key);

    Debug::assert(this.metaData() != null);

    // создаем привязки полей к колонкам resultSet
    binds  = new Map(Types::String, Types::Class);           // поля с привязкой
    dt = SysDictTable::newTableId(rec.tableId);
    for(i=1; i<=dt.columnCnt(); ++i )
    {
        df = new SysDictField(dt.id(), dt.column2id(i));
        columnName = this.nameByField(df, formatColumnName);
        if( this.copyAllowed(columnName, df) )
            binds.insert(columnName, df);                    // добавляем только те поля, которые имеют привязку к resultSet
    }

    bindsAll.insert(key, binds);
    return binds;
}